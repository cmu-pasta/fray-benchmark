plugins {
    id 'java'
    id 'de.undercouch.download' version '5.5.0'
}

group = 'cmu.pasta.sfuzz.benchmark'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    implementation fileTree(dir: "${buildDir}/libs/unzipped", include: ['*.jar'])

    // Only needed if we want to run benchmark without harness. This is only for compile (don't need at runtime because
    // this dependency is maintained by the harness at runtime.
    // See more: https://github.com/dacapobench/dacapobench/blob/main/benchmarks/harness/src/org/dacapo/harness/DacapoClassLoader.java
    compileOnly files(findJars(file("${buildDir}/libs/unzipped/dacapo-23.11-chopin/jar/lusearch")))
}

task downloadDacapo(type: Download) {
    src 'https://download.dacapobench.org/chopin/dacapo-23.11-chopin.zip'
    dest new File(buildDir, 'libs/dacapo.zip')
    onlyIfModified true
}

def findJars(File dir) {
    def jars = []
    if (dir.exists() && dir.isDirectory()) {
        dir.traverse(type: groovy.io.FileType.FILES, nameFilter: ~/.*\.jar/) { file ->
            jars.add(file)
        }
    }
    return jars
}

task unzipDacapo(type: Copy) {
    dependsOn downloadDacapo

    from zipTree("${buildDir}/libs/dacapo.zip")
    into "${buildDir}/libs/unzipped"
}


test {
    useJUnitPlatform()
}

task generateBuildInfo() {
    group = "JPF Build Resources"
    description = "Generates the build.properties file."
    doLast {

        Properties info = new Properties()

        def status = "git status --short".execute().text.trim()
        def revision = "git rev-parse --short HEAD".execute().text.trim()
        def userName = ["git", "log", "-1", "--format=%an <%ae>"].execute().text.trim()
        def date = "git log -1 --format=%ci".execute().text.trim()

        info.setProperty("revision", revision)
        info.setProperty("date", date)
        info.setProperty("author", userName)
        info.setProperty("os.arch", System.getProperty("os.arch"))
        info.setProperty("os.name", System.getProperty("os.name"))
        info.setProperty("user.country", System.getProperty("user.country"))
        info.setProperty("java.version", System.getProperty("java.version"))

        def writer = new File("build.properties").newWriter("utf-8")
        info.store(writer, "JPF core build info")
        writer.close()
    }
}

compileJava.dependsOn(unzipDacapo, generateBuildInfo)
